-- | This Alex file was machine-generated by the BNF converter and then trimmed 
-- and optimised by our team
{
{-# OPTIONS -fno-warn-incomplete-patterns #-}
{-# OPTIONS_GHC -w #-}
module Bnfc.LexWacc where

import qualified Data.Bits
import Data.Word (Word8)
import Data.Char (ord)
import Data.List (reverse)
}

-- | Alex lexer file
-- It creates tokens based on input.
-- Our base tokens contain a position that we carry all the way in the ADT to
-- be able to send precise semantic errors

-- List of tokens: 
$d = [0-9]                              -- ^ digit
$u = [\0-\255]                          -- ^ universal: any character
$s = [a-z\222-\255] # [\247]            -- ^ lowercase letter
$l = [a-zA-Z\192 - \255] # [\215 \247]  -- ^ letter


@rsyms =    -- ^ symbols and non-identifier-like reserved words
   \, | \;

:-
"#" [.]* ;  -- ^ Toss single line comments

-- | These are all the keywords token generation code, wrapped in a position
$white+ ;
\,                  { (\p s -> PT p T_CoT) }
\;                  { (\p s -> PT p T_SepT) }
e n d               { (\p s -> PT p T_EndT) }
b e g i n           { (\p s -> PT p T_BeginT) }
s k i p             { (\p s -> PT p T_SkipT) }
r e a d             { (\p s -> PT p T_ReadT) }
p r i n t           { (\p s -> PT p T_PrintT) }
p r i n t l n       { (\p s -> PT p T_PrintLnT) }
f r e e             { (\p s -> PT p T_FreeT) }
e x i t             { (\p s -> PT p T_ExitT) }
$d +                { createDigit }
\+                  { (\p s -> PT p T_PlusToken) } 
\-                  { (\p s -> PT p T_MinusToken) } 
t r u e | f a l s e { (\p s -> PT p (T_BoolLiteral s)) }
i n t               { (\p s -> PT p T_IntT) }
b o o l             { (\p s -> PT p T_BoolT) }
c h a r             { (\p s -> PT p T_CharT) }
s t r i n g         { (\p s -> PT p T_StringT) }
\*                  { (\p s -> PT p T_TimesT) }
\/                  { (\p s -> PT p T_DivideT) }
\%                  { (\p s -> PT p T_ModuloT) }
\>                  { (\p s -> PT p T_GreaterT) }
\<                  { (\p s -> PT p T_LessT) }
\> \=               { (\p s -> PT p T_GreaterEqT) }
\< \=               { (\p s -> PT p T_LessEqT) }
\= \=               { (\p s -> PT p T_EqT) }
\! \=               { (\p s -> PT p T_NotEqT) }
\& \&               { (\p s -> PT p T_AndT) }
\| \|               { (\p s -> PT p T_OrT) }
\(                  { (\p s -> PT p T_LParenT) }
\)                  { (\p s -> PT p T_RParenT) }
\[                  { (\p s -> PT p T_LBracketT) }
\]                  { (\p s -> PT p T_RBracketT) }
i s                 { (\p s -> PT p T_IsT) }
w h i l e           { (\p s -> PT p T_WhileT) }
d o                 { (\p s -> PT p T_DoT) }
d o n e             { (\p s -> PT p T_DoneT) }
i f                 { (\p s -> PT p T_IfT) }
f i                 { (\p s -> PT p T_FiT) }
t h e n             { (\p s -> PT p T_ThenT) }
e l s e             { (\p s -> PT p T_ElseT) }
p a i r             { (\p s -> PT p T_PairT) }
n e w p a i r       { (\p s -> PT p T_NewpairT) }
c a l l             { (\p s -> PT p T_CallT) }
f s t               { (\p s -> PT p T_FstT) }
s n d               { (\p s -> PT p T_SndT) }
\=                  { (\p s -> PT p T_EqualT) }
l e n               { (\p s -> PT p T_LenT) }
o r d               { (\p s -> PT p T_OrdT) }
c h r               { (\p s -> PT p T_ChrT) }
r e t u r n         { (\p s -> PT p T_ReturnT) }
\!                  { (\p s -> PT p T_NotT) }
n u l l             { (\p s -> PT p T_PairLiteral) }

-- | Special tokens matched with regualr exp
\' ($u # [\' \\ \"]| \\ [\' \\ n t 0 b f \"]) \'  { (\p s -> PT p ((T_CharLiteral ) s)) } -- ^ Char token
\" ($u # [\' \\ \"]| \\ [\' \\ n t 0 b f \"]) * \"{ (\p s -> PT p ((T_StringLiteral ) s)) } -- ^ String token
(\_ | $s)($l | $d | \_)*                          { (\p s -> PT p ((T_Identifier ) s)) } -- ^ Identifier token

{
-- | Create digit safely create a digit checking for overflow
createDigit :: Posn    -- ^ Position of the checked token
            -> String  -- ^ String 
            -> Token   -- ^ Token returned, might be an Err token
createDigit p s = 
  if checkBound s 
    then Err p 
    else PT p (T_IntDigit s)

-- | Value of min/max supported integer
bound :: Integer -- ^ @ 2^31 @ 32 bit signed int
bound = 2147483648

-- | Checks overflow and underflow assignments
checkBound :: String -> Bool
checkBound s = (read s :: Integer) > bound

share :: String -> String
share = id

-- | Data type declaration for all the tokens used in WACC
data Tok
 = T_CoT
 | T_SepT	
 | T_EndT
 | T_BeginT 
 | T_SkipT 
 | T_ReadT 
 | T_PrintT 
 | T_PrintLnT 
 | T_FreeT 
 | T_ExitT 
 | T_IntDigit !String 
 | T_PlusToken 
 | T_MinusToken 
 | T_BoolLiteral !String 
 | T_IntT 
 | T_BoolT 
 | T_CharT 
 | T_StringT 
 | T_TimesT 
 | T_DivideT 
 | T_ModuloT 
 | T_GreaterT 
 | T_LessT 
 | T_GreaterEqT 
 | T_LessEqT 
 | T_EqT 
 | T_NotEqT 
 | T_AndT 
 | T_OrT 
 | T_LParenT 
 | T_RParenT 
 | T_LBracketT 
 | T_RBracketT 
 | T_IsT 
 | T_WhileT 
 | T_DoT 
 | T_DoneT 
 | T_IfT 
 | T_FiT 
 | T_ThenT 
 | T_ElseT 
 | T_PairT 
 | T_NewpairT 
 | T_CallT 
 | T_FstT 
 | T_SndT 
 | T_EqualT 
 | T_LenT 
 | T_OrdT 
 | T_ChrT 
 | T_ReturnT 
 | T_NotT 
 | T_PairLiteral
 | T_CharLiteral !String
 | T_StringLiteral !String
 | T_Identifier !String

 deriving (Eq,Show,Ord)

-- | Token type declaration it can be made of:
data Token
 = PT Posn Tok -- ^ Tok is raw identifier of the token wrapped in a PosiTion 
 | Err Posn    -- ^ Err if illegal token (like Int Overflow)
  deriving (Eq,Show,Ord)

-- | Pretty prints position
tokenPos :: [Token] -> String
tokenPos (PT (Pn _ l c) _ :_) = "at (" ++ show l ++ ", " ++ show c ++ ")"
tokenPos (Err (Pn _ l c) :_) = "at (" ++ show l ++ ", " ++ show c ++ ")"
tokenPos _ = "at end of file"

-- | Starps Token to return position
tokenPosn :: Token -> Posn
tokenPosn (PT p _) = p
tokenPosn (Err p) = p

-- | Converts Token into a position pair
tokenLineCol :: Token -> (Int, Int)
tokenLineCol = posLineCol . tokenPosn

posLineCol :: Posn -> (Int, Int)
posLineCol (Pn _ l c) = (l,c)


-- | Print Token
-- When parsing is not successfull it prints the token converting them back to
-- their string representation in the program
prToken :: Token -- ^ The token to convert
        -> String -- ^ original string representation
prToken (Err _)
  = " probably due to an int overflow"
prToken t = case t of
  PT _ T_CoT -> ","
  PT _ T_SepT -> ";"
  PT _ T_EndT -> "end"
  PT _ T_BeginT -> "begin"
  PT _ T_SkipT -> "skip"
  PT _ T_ReadT -> "read"
  PT _ T_PrintT -> "print"
  PT _ T_PrintLnT -> "println"
  PT _ T_FreeT -> "free"
  PT _ T_ExitT -> "exit"
  PT _ (T_IntDigit s) -> s
  PT _ T_PlusToken -> "+"
  PT _ T_MinusToken -> "-"
  PT _ (T_BoolLiteral "true") -> "true"
  PT _ (T_BoolLiteral "false") -> "false"
  PT _ T_IntT -> "int"
  PT _ T_BoolT -> "bool"
  PT _ T_CharT -> "char"
  PT _ T_StringT -> "string"
  PT _ T_TimesT -> "*"
  PT _ T_DivideT -> "/"
  PT _ T_ModuloT -> "%"
  PT _ T_GreaterT -> ">"
  PT _ T_LessT -> "<"
  PT _ T_GreaterEqT -> ">="
  PT _ T_LessEqT -> "<="
  PT _ T_EqT -> "=="
  PT _ T_NotEqT -> "!="
  PT _ T_AndT -> "&&"
  PT _ T_OrT -> "||"
  PT _ T_LParenT -> ""
  PT _ T_RParenT -> ""
  PT _ T_LBracketT -> "["
  PT _ T_RBracketT -> "]"
  PT _ T_IsT -> "is"
  PT _ T_WhileT -> "while"
  PT _ T_DoT -> "do"
  PT _ T_DoneT -> "done"
  PT _ T_IfT -> "if"
  PT _ T_FiT -> "fi"
  PT _ T_ThenT -> "then"
  PT _ T_ElseT -> "else"
  PT _ T_PairT -> "pair"
  PT _ T_NewpairT -> "newpair"
  PT _ T_CallT -> "call"
  PT _ T_FstT -> "fst"
  PT _ T_SndT -> "snd"
  PT _ T_EqualT -> "="
  PT _ T_LenT -> "len"
  PT _ T_OrdT -> "ord"
  PT _ T_ChrT -> "chr"
  PT _ T_ReturnT -> "return"
  PT _ T_NotT -> "!"
  PT _ T_PairLiteral -> "null"
  PT _ (T_CharLiteral s) -> s
  PT _ (T_StringLiteral s) -> s
  PT _ (T_Identifier s) -> s

-------------------------------------------------------------------
-- | Alex wrapper code.
-- A modified "posn" wrapper. 
-- Walks the file assuming UTF-8 encoding (for extension?) 
-------------------------------------------------------------------

data Posn = Pn !Int !Int !Int
      deriving (Eq, Show,Ord)

alexStartPos :: Posn
alexStartPos = Pn 0 1 1

alexMove :: Posn -> Char -> Posn
alexMove (Pn a l c) '\t' = Pn (a+1)  l     (((c+7) `div` 8)*8+1)
alexMove (Pn a l c) '\n' = Pn (a+1) (l+1)   1
alexMove (Pn a l c) _    = Pn (a+1)  l     (c+1)

type Byte = Word8

type AlexInput = (Posn,     -- current position
                  Char,     -- previous char
                  [Byte],   -- pending bytes on the current char
                  String)   -- current input string

tokens :: String -> [Token]
tokens str = go (alexStartPos, '\n', [], str)
    where
      go :: AlexInput -> [Token]
      go inp@(pos, _, _, str) =
               case alexScan inp 0 of
                AlexEOF                   -> []
                AlexError (pos, _, _, _)  -> [Err pos]
                AlexSkip  inp' len        -> go inp'
                AlexToken inp' len act    -> act pos (take len str) : (go inp')

alexGetByte :: AlexInput -> Maybe (Byte,AlexInput)
alexGetByte (p, c, (b:bs), s) = Just (b, (p, c, bs, s))
alexGetByte (p, _, [], s) =
  case  s of
    []  -> Nothing
    (c:s) ->
             let p'     = alexMove p c
                 (b:bs) = utf8Encode c
              in p' `seq` Just (b, (p', c, bs, s))

alexInputPrevChar :: AlexInput -> Char
alexInputPrevChar (p, c, bs, s) = c

-- | Encode a Haskell String to a list of Word8 values, in UTF8 format.
utf8Encode :: Char -> [Word8]
utf8Encode = map fromIntegral . go . ord
 where
  go oc
   | oc <= 0x7f       = [oc]

   | oc <= 0x7ff      = [ 0xc0 + (oc `Data.Bits.shiftR` 6)
                        , 0x80 + oc Data.Bits..&. 0x3f
                        ]

   | oc <= 0xffff     = [ 0xe0 + (oc `Data.Bits.shiftR` 12)
                        , 0x80 + ((oc `Data.Bits.shiftR` 6) Data.Bits..&. 0x3f)
                        , 0x80 + oc Data.Bits..&. 0x3f
                        ]
   | otherwise        = [ 0xf0 + (oc `Data.Bits.shiftR` 18)
                        , 0x80 + ((oc `Data.Bits.shiftR` 12) Data.Bits..&. 0x3f)
                        , 0x80 + ((oc `Data.Bits.shiftR` 6) Data.Bits..&. 0x3f)
                        , 0x80 + oc Data.Bits..&. 0x3f
                        ]
}
